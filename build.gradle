apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: "com.vanniktech.android.junit.jacoco"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.2.0'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.4.0'

        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }
}

codeQualityTools {
    lint {
        textReport true
    }
}

junitJacoco {
    ignoreProjects = ['onactivityresult-sample'] // Don't care about sample
}

allprojects {
    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

ext {
    isCI = 'true'.equals(System.getProperty('CI'))

    minSdkVersion = 15
    compileSdkVersion = 23
    targetSdkVersion = 23
    buildToolsVersion = '23.0.3'

    shouldPreDex = !isCI

    supportLibraryVersion = '23.3.0'

    intellijAnnotations = "com.intellij:annotations:12.0"
    supportAnnotations = "com.android.support:support-annotations:$supportLibraryVersion"
    supportAppcompat = "com.android.support:appcompat-v7:$supportLibraryVersion"
    supportDesign = "com.android.support:design:$supportLibraryVersion"

    butterknifeVersion = '8.0.1'
    butterknife = "com.jakewharton:butterknife:${butterknifeVersion}"
    butterknifeCompiler = "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    google = [
            autoservice   : 'com.google.auto.service:auto-service:1.0-rc2',
            compiletesting: 'com.google.testing.compile:compile-testing:0.9'
    ]

    testing = [
            android           : 'com.google.android:android:4.1.1.4',
            junit             : 'junit:junit:4.12',
            mockito           : 'org.mockito:mockito-core:2.0.52-beta',
            robolectric       : 'org.robolectric:robolectric:3.0',
            privateConstructor: 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0'
    ]

    square = [
            javapoet: 'com.squareup:javapoet:1.6.1'
    ]

    javaVersion = JavaVersion.VERSION_1_7
}

subprojects { project ->
    version = VERSION_NAME
    group = GROUP

    repositories {
        jcenter()
        mavenCentral()
    }

    project.plugins.apply('net.ltgt.errorprone')

    if (!rootProject.ext.isCI) {
        tasks.matching { it instanceof Test }.all {
            testLogging.events = ["failed", "passed", "skipped"]
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}