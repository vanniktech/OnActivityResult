buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as int
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    defaultConfig {
        applicationId 'com.vanniktech.onactivityresult.sample'
        minSdkVersion rootProject.ext.minSdkVersion as int
        targetSdkVersion rootProject.ext.targetSdkVersion as int
        versionCode 1
        versionName '1.0'
    }

    def shouldSign = project.hasProperty('RELEASE_STORE_FILE') && project.hasProperty('RELEASE_STORE_PASSWORD') && project.hasProperty('RELEASE_KEY_ALIAS') && project.hasProperty('RELEASE_KEY_PASSWORD')

    if (shouldSign) {
        signingConfigs {
            release {
                // http://stackoverflow.com/a/21020469/1979703
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion as JavaVersion
        targetCompatibility rootProject.ext.javaVersion as JavaVersion
    }

    dexOptions {
        preDexLibraries = rootProject.ext.shouldPreDex
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            minifyEnabled true
        }
    }
}

dependencies {
    compile project(':onactivityresult')
    apt project(':onactivityresult-compiler')

    // compile 'com.vanniktech:onactivityresult:0.6.0'
    // apt 'com.vanniktech:onactivityresult-compiler:0.6.0'

    compile rootProject.ext.butterknife
    apt rootProject.ext.butterknifeCompiler

    compile rootProject.ext.supportAppcompat
    compile rootProject.ext.supportDesign
}
